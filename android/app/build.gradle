def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new FileNotFoundException()("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

// Configure gradle to use your upload key when building your app in release mode
// see more in https://flutter.dev/docs/deployment/android#configure-signing-in-gradle
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
// apply plugin firebase
// see more in https://firebase.flutter.dev/docs/installation/android
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 31

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.hugeman.flutterstarterkit"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        // Android Device Testing (firebase test lab) uses androidx's version of AndroidJUnitRunner and has androidx libraries as a dependency.
        // see more in https://github.com/flutter/flutter/tree/master/packages/integration_test#android-device-testing
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // if your minSdkVersion is set to 20 or lower, then you must use the multidex support library
        // see more in https://firebase.flutter.dev/docs/installation/android, https://developer.android.com/studio/build/multidex#mdex-gradle
        multiDexEnabled true
    }

    // Configure gradle to use your upload key when building your app
    // see more in https://flutter.dev/docs/deployment/android#configure-signing-in-gradle
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug { // for mode debug
            signingConfig signingConfigs.debug
        }
        release { // for distribute app (with credential to deploy play store)
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android Device Testing (firebase test lab) uses androidx's version of AndroidJUnitRunner and has androidx libraries as a dependency.
    // see more in https://github.com/flutter/flutter/tree/master/packages/integration_test#android-device-testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // if your minSdkVersion is set to 20 or lower, then you must use the multidex support library
    // see more in https://firebase.flutter.dev/docs/installation/android, https://developer.android.com/studio/build/multidex#mdex-gradle
    implementation 'com.android.support:multidex:1.0.3'

    // Add Firebase SDKs to your app
    // see more in https://firebase.google.com/docs/android/setup?authuser=0
    implementation platform('com.google.firebase:firebase-bom:28.4.2')

    // Add Firebase performance monitoring to your app
    // see more in https://firebase.google.com/docs/perf-mon/get-started-android
    implementation 'com.google.firebase:firebase-perf'
}
